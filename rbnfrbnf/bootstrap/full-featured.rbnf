# lexer definition
auto_const := '::=' ':=' '<<' '->' '=>'
auto_const := '=' '<' '>' '/' '|' '{' '}' '[' ']' '(' ')' '+' '*' '.' ',' ';' '?' ':'
Str        := R'[A-Z]\'([^\\\']+|\\.)*?\'|\'([^\\\']+|\\.)*?\''
Name       := R'[a-zA-Z_\u4e00-\u9fa5][a-zA-Z0-9_\u4e00-\u9fa5]*'
Int        := R'\d+'
Space      := R'\s+'
Comment    := R'(#.*)|(((/\*)+?[\w\W]+?(\*/)+))'
kword cast := 'rewrite' 'when' 'import' 'extern' 'constvalue' 'recur'

# helper parser definition
ignore [Comment Space]

# syntax node parser definition
Lexer      ::=
    [is_const='constvalue'] name=Name ':=' (lexers=Str+ | extern_mark='extern') ';'
    -> LexerC(name, extern_mark or [LiteralC(lexer) for lexer in lexers], bool(is_const))

Parser    ::=
    name=Name '::='
    ['|']
    impl=ADTParser
    ';'
    -> ParserC(name, impl)

ADTParser ::=
    | cases<<Case
      ('|' cases<<Case)*
    | single=AndParser
    -> single or ADTParserC(cases)

Case ::=
    name=Name ':' impl=AndParser
    -> CaseC(name, impl)

OrParser ::=
    brs<<AndParser ('|' brs<<AndParser)*
    -> OrParserC(brs) if len(brs) is not 1 else brs[0]

AndParser ::=
    (exprs<<(Predicate | ClauseExpr))+
    -> AndParserC(exprs) if len(exprs) is not 1 else exprs[0]

ClauseExpr::=
    [bind_name=Name (bind='=' | push='<<')]
    expr=Expr ['{' rep=(Int{1 2}) '}'
              | star='*'
              | plus='+'
              | '=>' rew=Name]
    ->
        if rep:
            expr = RepC(rep, expr)
        elif star:
            expr = StarC(expr)
        elif plus:
            expr = PlusC(expr)
        elif rew:
            expr = RewriteC(rew, expr)
        if bind_name:
            expr = (BindC if bind else PushC)(bind_name, expr)
        return expr

Predicate ::=
    | '?' predicate=Name
    -> PredicateC(predicate)
Expr ::=
    | '(' nested=OrParser ')'
    | '[' optional=OrParser ']'
    | '?' predicate=Name
    | ref=Name
    | literal=Str
    ->
        if nested:
            return nested
        if optional:
            return OptionalC(optional)
        if ref:
            return RefC(ref)
        if literal:
            return LiteralC(literal)

Module ::= (';' | suites <<(Lexer | Parser))+ -> ModuleC(suites);

